TEMPLATE = lib
TARGET = calligrafilters

include($${TOP_SOURCE_DIR}/common.pri)

#QT += core gui xml svg sql network widgets
QT += core gui xml

CALLIGRAFILTERS_DIR = $${TOP_SOURCE_DIR}/calligrafilters
CALLIGRAFILTERS_LIBMSO_DIR = $${TOP_SOURCE_DIR}/../filters/libmso
CALLIGRAFILTERS_MSWORD_DIR = $${TOP_SOURCE_DIR}/../filters/words/msword-odf
CALLIGRAFILTERS_LIBMSOOXML_DIR = $${TOP_SOURCE_DIR}/../filters/libmsooxml
CALLIGRAFILTERS_DOCX_DIR = $${TOP_SOURCE_DIR}/../filters/words/docx
CALLIGRAFILTERS_XLSX_DIR = $${TOP_SOURCE_DIR}/../filters/sheets/xlsx

INCLUDEPATH = \
     $${CALLIGRAFILTERS_DIR} \
     $${CALLIGRAFILTERS_LIBMSO_DIR} \
     $${CALLIGRAFILTERS_LIBMSO_DIR}/generated/ \
     $${CALLIGRAFILTERS_MSWORD_DIR} \
     $${CALLIGRAFILTERS_MSWORD_DIR}/wv2/src/ \
     $${CALLIGRAFILTERS_LIBMSOOXML_DIR} \
     $${CALLIGRAFILTERS_DOCX_DIR} \
     $${CALLIGRAFILTERS_XLSX_DIR} \
     $$INCLUDEPATH

LIBS += -lcalligralibs

# defines needed for msword doc
DEFINES += "HAVE_DLFCN_H=1"
DEFINES += HAVE_ERRNO_H
DEFINES += "HAVE_FLOAT_H=1"
DEFINES += HAVE_FUNC_FINITE
DEFINES += HAVE_FUNC_ISINF
DEFINES += HAVE_FUNC_ISNAN
DEFINES += "HAVE_ICONV_H=1"
DEFINES += "HAVE_INTTYPES_H=1"
DEFINES += "HAVE_MATH_H=1"
DEFINES += "HAVE_MEMORY_H=1"
DEFINES += "HAVE_STDINT_H=1"
DEFINES += "HAVE_STDLIB_H=1"
DEFINES += "HAVE_STRINGS_H=1"
DEFINES += "HAVE_STRING_H=1"
DEFINES += "HAVE_SYS_STAT_H=1"
DEFINES += "HAVE_SYS_TYPES_H=1"
DEFINES += "HAVE_UNISTD_H=1"
DEFINES += "SIZEOF_CHAR=sizeof\\(char\\)"
DEFINES += "SIZEOF_INT=sizeof\\(int\\)"
DEFINES += "SIZEOF_LONG=sizeof\\(long\\)"
DEFINES += "SIZEOF_SHORT=sizeof\\(short\\)"
DEFINES += "SIZEOF_VOID_P=sizeof\\(void*\\)"
DEFINES += "ICONV_CONST="

# for pre-generated files and stuff
#SOURCES += $$files($$CALLIGRAFILTERS_DIR/*.cpp)
#HEADERS += $$files($$CALLIGRAFILTERS_DIR/*.h)
#HEADERS -= $$files($$CALLIGRAFILTERS_DIR/config.h)

# libmso
SOURCES += $$files($$CALLIGRAFILTERS_LIBMSO_DIR/*.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_LIBMSO_DIR/*.h)
SOURCES += $$files($$CALLIGRAFILTERS_LIBMSO_DIR/generated/*.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_LIBMSO_DIR/generated/*.h)

# doc
SOURCES += $$files($$CALLIGRAFILTERS_MSWORD_DIR/*.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_MSWORD_DIR/*.h)
SOURCES += $$files($$CALLIGRAFILTERS_MSWORD_DIR/wv2/src/*.cpp)
SOURCES += $$files($$CALLIGRAFILTERS_MSWORD_DIR/wv2/src/crc32.c)
HEADERS += $$files($$CALLIGRAFILTERS_MSWORD_DIR/wv2/src/*.h)

# libmsooxml
SOURCES += $$files($$CALLIGRAFILTERS_LIBMSOOXML_DIR/*.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_LIBMSOOXML_DIR/*.h)

# docx
SOURCES += $$files($$CALLIGRAFILTERS_DOCX_DIR/*.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_DOCX_DIR/*.h)

# for number formats
SOURCES += $$files($$CALLIGRAFILTERS_XLSX_DIR/NumberFormatParser.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_XLSX_DIR/NumberFormatParser.h)

# for charting
SOURCES += $$files($$CALLIGRAFILTERS_XLSX_DIR/ChartExport.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_XLSX_DIR/ChartExport.h)
HEADERS += $$files($$CALLIGRAFILTERS_XLSX_DIR/Charting.h)
SOURCES += $$files($$CALLIGRAFILTERS_XLSX_DIR/XlsxXmlChartReader.cpp)
HEADERS += $$files($$CALLIGRAFILTERS_XLSX_DIR/XlsxXmlChartReader.h)

mocWrapper(HEADERS)
